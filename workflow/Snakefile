import yaml
quarto_yml = '_quarto.yml'
with open(quarto_yml, 'r') as infile:
    quarto_meta = yaml.full_load(infile)

pdf_output = f"{quarto_meta['project']['output-dir']}/{quarto_meta['book']['output-file']}.pdf"

rule targets:
    input:
        pdf_output

rule update_extension:
    output:
        directory("_extensions/kelly-sovacool/rackham/")
    conda: "envs/env.yml"
    shell:
        """
        quarto update extension kelly-sovacool/rackham
        """

rule render_pdf:
    input:
        yml = quarto_yml,
        qmd = quarto_meta['book']['chapters'],
        bib = 'references.bib',
        tex = 'header.tex',
        ext = rules.update_extension.output
    output:
        pdf = pdf_output,
        tex = f"{quarto_meta['book']['output-file']}.tex"
    conda: "envs/env.yml"
    shell:
        """
        quarto render --to rackham-pdf
        """

rule render_readme:
    input: qmd='README.qmd'
    output: md='README.md'
    conda: "envs/env.yml"
    shell:
        """
        quarto render {input.qmd}
        """
